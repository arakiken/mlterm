# -*- mode:sh; sh-basic-offset:2; tab-width:2; indent-tabs-mode:t -*-
AC_INIT()

AC_CONFIG_AUX_DIR(script)

AC_CANONICAL_HOST
AC_CANONICAL_BUILD

# for CFLAGS="..." ./configure ...
AC_SUBST(CFLAGS)

AC_PROG_CC
AC_C_INLINE
AC_PROG_INSTALL

AC_LIBTOOL_WIN32_DLL
AC_ARG_WITH(libtool,
	[  --with-libtool@<:@=ARG@:>@    libtool path @<:@default=without@:>@],
	libtool=$with_libtool)
if test "${libtool}" != "" ; then
	LIBTOOL=${libtool}
else
	AM_PROG_LIBTOOL
	LIBTOOL='${top_builddir}/libtool'
fi
AC_SUBST(LIBTOOL)

if test -d ${top_srcdir-$srcdir}/baselib ; then
	POBL_BUILDDIR='${top_builddir}/baselib'
	SUBDIRS="baselib"
elif test -d ${top_srcdir-$srcdir}/../baselib ; then
	POBL_BUILDDIR='${top_builddir}/../baselib'
	SUBDIRS="../baselib"
else
	POBL_BUILDDIR=""
	SUBDIRS=""
fi
POBL_CFLAGS="-I${POBL_BUILDDIR}/include"
AC_SUBST(POBL_CFLAGS)

AC_ARG_ENABLE(debug,
	[  --enable-debug          debug @<:@default=disabled@:>@],
	debug=$enable_debug)
if test "$debug" = "yes" ; then
	DEB_CFLAGS="-DDEBUG -DBL_DEBUG"
fi
AC_SUBST(DEB_CFLAGS)

AC_ARG_WITH(map-table,
	[  --without-map-table     mapping table @<:@default=with@:>@],
	map_table=$with_map_table,map_table="yes")
if test "$map_table" = "no" ; then
	TABLE_CFLAGS="${TABLE_CFLAGS} -DREMOVE_MAPPING_TABLE"
fi

AC_ARG_WITH(prop-table,
	[  --without-prop-table    property table @<:@default=with@:>@],
	prop_table=$with_prop_table,prop_table="yes")
if test "$prop_table" = "no" ; then
	TABLE_CFLAGS="${TABLE_CFLAGS} -DREMOVE_PROPERTY_TABLE"
fi

AC_ARG_ENABLE(dl-table,
	[  --disable-dl-table      dynamic loading table @<:@default=enable@:>@],
	dl_table=$enable_dl_table,dl_table="yes")
if test "$dl_table" = "no" ; then
	TABLE_CFLAGS="${TABLE_CFLAGS} -DNO_DYNAMIC_LOAD_TABLE"
	MAKE_DIRS="src"
else
	MAKE_DIRS="src module"
fi
AC_SUBST(TABLE_CFLAGS)
AC_SUBST(MAKE_DIRS)

#
# --- check for undefined symbol ---
#
AC_MSG_CHECKING([for undefined symbol])
if test "x$allow_undefined_flag" = "xunsupported" ; then
	AC_MSG_RESULT([not supported])
	NO_UNDEFINED_FLAG="-no-undefined"
	# link ef_char.lo to module/*.dll to resolve addresses in creating dll.
	EF_CHAR_LO="../src/ef_char.lo"
	if test "x${POBL_BUILDDIR}" != "x" ; then
		POBL_LIBS="${POBL_BUILDDIR}/src/libpobl.la"
	else
		POBL_LIBS='-lpobl'
	fi
else
	AC_MSG_RESULT(supported)
	NO_UNDEFINED_FLAG=""
	EF_CHAR_LO=""
	POBL_LIBS=""
fi
AC_SUBST(NO_UNDEFINED_FLAG)
AC_SUBST(EF_CHAR_LO)
AC_SUBST(POBL_LIBS)


if test "$with_gnu_ld" = "yes" ; then
	DEXPORT="-Wl,--version-script=\$(top_srcdir)/src/dexport.map"
fi
AC_SUBST(DEXPORT)


# This should be uncommented if mef and pobl alone are distributed in a package.
#if test "${SUBDIRS}" != "" ; then
#	AC_CONFIG_SUBDIRS(${SUBDIRS})
#fi

AC_OUTPUT(Makefile src/Makefile module/Makefile)
